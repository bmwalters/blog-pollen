#lang racket

;; Generates a pagetree by sorting posts in posts-sources-dir
;; by latest-first using the @created{YYYY-MM-DD} tag.
;; The resulting pagetree is suitable for a feed or sitemap.

(require racket/runtime-path)
(require racket/string)

(require pollen/core)
(require pollen/file)
(require pollen/setup)  ; for pollen-template-prefix

(define-runtime-path posts-sources-dir ".")

(define (get-post-doc path)
  (get-doc (build-path posts-sources-dir path)))

(define (select-created-at p)
  (select 'created p))

(define (template-file? path)
  (define name (path->string (file-name-from-path path)))
  (string-prefix? name pollen-template-prefix))

;; A post is a pollen source that's not a template
;; (A file is a pollen source if ->output-path transforms it)
(define (post-source? path)
  (and (not (equal? path (->output-path path)))
       (not (template-file? path))))

(define posts
  (let* ([posts-input-paths
          (filter post-source?
                  (directory-list posts-sources-dir))]
         [posts-sorted
          (sort posts-input-paths
            (lambda (p1 p2) string>?)
            #:key (compose select-created-at get-post-doc))])
    (map (compose string->symbol path->string ->output-path) posts-sorted)))

(provide doc)
(define doc `(pagetree-root ,@posts))
